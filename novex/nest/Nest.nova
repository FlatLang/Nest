package "novex/nest"

import "nova/io/FancyOutputStream"
import "nova/primitive/number/RealNumber"

class Nest<X> {
    visible static final FancyOutputStream out = new FancyOutputStream()
    
    X data
    
    construct(this X data) {}
    
    public toBe(X other, var String message = null) => true {
        if (other != data) {
            message = message ?: "#data != #other"
            
            throw new InvalidAssertionException(message)
            
            return false
        }
    }
    
    // or
    
    public toNotBe(X other, var String message = null) => true {
        if (other == data) {
            message = message ?: "#data == #other"
            
            throw new InvalidAssertionException(message)
            
            return false
        }
    }
    
    [RequireGenericType X extends Double]
    public toBeWithinToleranceOf(Double target, passing Double tolerance, var String message = null) => true {
        if (!Double.withinTolerance((Double)data, target, tolerance)) {
            message = message ?: "#data not within tolerance of #target"
            
            throw new InvalidAssertionException(message)
            
            return false
        }
    }
    
    public static expectValue(Object value, String message = "Value is null") {
        if (!value) {
            fail(message)
        }
    }
    
    public static expectNull(Object value, String message = "Value is not null") {
        if (value) {
            fail(message)
        }
    }
    
    public static expectTrue(Bool condition, String message = "Value not true") {
        if (!condition) {
            fail(message)
        }
    }
    
    public static expectFalse(Bool condition, String message = "Value not false") {
        if (condition) {
            fail(message)
        }
    }
    
    public static fail(String message = "Failure") {
        throw new InvalidAssertionException(message)
    }
    
    public static expect<T>(T obj) => new Nest<T>(obj)
}
